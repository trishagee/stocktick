// doesn't work with the new plugin syntax: https://github.com/google/osdetector-gradle-plugin/issues/15
buildscript {
    repositories {
        mavenCentral()
    }
    dependencies {
        classpath 'com.google.gradle:osdetector-gradle-plugin:1.6.0'
    }
}
//plugins {
//    id 'com.google.osdetector' version '1.6.0'
//}

apply plugin: 'application'
apply plugin: 'com.google.osdetector'

sourceCompatibility = JavaVersion.VERSION_11
group = 'com.example.demo'
version = '0.0.1-SNAPSHOT'
mainClassName = 'com.mechanitis.demo.client.StockTicker'

repositories {
    mavenCentral()
}

ext.platform = osdetector.os == 'osx' ? 'mac' : osdetector.os

dependencies {
    compile "org.openjfx:javafx-base:11:$platform"
    compile "org.openjfx:javafx-graphics:11:$platform"
    compile "org.openjfx:javafx-controls:11:$platform"
    compile "org.openjfx:javafx-fxml:11:$platform"

    compile 'org.eclipse.jetty.websocket:javax-websocket-server-impl:9.4.6.v20170531'

    implementation 'org.springframework:spring-webflux:5.0.9.RELEASE'
    implementation 'io.projectreactor:reactor-core:3.2.1.RELEASE'
    implementation 'io.projectreactor.ipc:reactor-netty:0.7.10.RELEASE'

}

//because Java 9
// not really sure why we need this if we're modular, but it doesn't seem to work from the command line without it.
compileJava {
    doFirst {
        options.compilerArgs = [
                '--module-path', classpath.asPath,
                '--add-modules', 'javafx.controls',
                '--add-modules', 'javafx.fxml'
        ]
    }
}

run {
    doFirst {
        jvmArgs = [
                '--module-path', classpath.asPath,
                '--add-modules', 'javafx.controls',
                '--add-modules', 'javafx.fxml'
        ]
    }
}

apply plugin: 'idea'
idea {
    module {
        inheritOutputDirs = true
    }
}